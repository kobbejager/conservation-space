PREFIX emf: <http://ittruse.ittbg.com/ontology/enterpriseManagementFramework#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT distinct ?instance (emf:ClassDescription as ?instanceType) ?classCategory
((if(!bound(?aDefinitionId) && (?isSearchable=true || ?isCreateable=true || ?isUploadable=true), "objectinstance", ?aDefinitionId)) as ?definitionId)
((if(!bound(?isSearchable), "false"^^xsd:boolean , ?isSearchable)) as ?searchable)
((if(!bound(?isCreateable), "false"^^xsd:boolean , ?isCreateable)) as ?createable)
((if(!bound(?isUploadable), "false"^^xsd:boolean , ?isUploadable)) as ?uploadable)
where {
    ?instance a owl:Class .
    OPTIONAL{
        ?instance emf:isSearchable ?isSearchable
    }
    OPTIONAL{
        ?instance emf:isCreateable ?isCreateable
    }
    OPTIONAL{
        ?instance emf:isUploadable ?isUploadable
    }
    OPTIONAL{
        {
            ?instance emf:classCategory ?classCategory.
        }
        UNION {
            FILTER(! EXISTS {
                ?instance emf:classCategory ?tempClassCategory.
            })
            ?instance rdfs:subClassOf ?superClass.
            ?superClass emf:classCategory ?classCategory.
        }
    }
    OPTIONAL{
        {
            ?instance emf:definitionId ?aDefinitionId.
        }
        UNION{
            FILTER(! EXISTS  {
                ?instance emf:definitionId ?tempDefId.
            })
            ?instance rdfs:subClassOf ?superClass.
            ?superClass emf:definitionId ?aDefinitionId.
        }
    }
    ?instance rdfs:label ?title ;
        skos:definition | rdfs:comment ?description .
}
