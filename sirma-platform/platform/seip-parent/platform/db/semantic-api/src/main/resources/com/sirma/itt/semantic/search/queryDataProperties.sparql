SELECT DISTINCT ?instance (owl:DatatypeProperty as ?instanceType) ?definition ?auditEvent 
?title ?creator ?subPropertyOf ?isSearchable ?isSystemProperty
(if (lang(?title)= "", "en", lang(?title)) as ?titleLanguage)
(if (lang(?definition)= "", "en", lang(?definition)) as ?definitionLanguage)
(if (lang(?creator)= "", "en", lang(?creator)) as ?creatorLanguage)
(if(?type = emf:DefinitionDataProperty, emf:DefinitionDataProperty, "") as ?propertyType)
((if (bound(?domainClass1), ?domainClass1, ptop:Entity)) as ?domainClass)
((if (bound(?rangeClass1), ?rangeClass1, xsd:string)) as ?rangeClass)
WHERE {
    {
        ?instance a owl:DatatypeProperty .
    } UNION {
        ?instance a emf:DefinitionDataProperty .
    }
    ?instance dcterms:title ?title ;
        rdf:type ?type.
    optional {
        ?instance dcterms:description ?definition .
    } optional {
        ?instance rdfs:domain ?domainClass1 .
    } optional {
        ?instance rdfs:range ?rangeClass1 .
    } optional {
        ?instance emf:auditEvent ?auditEvent .
    } optional {
        ?instance dc:creator ?creator .
    } optional {
        ?instance emf:isSearchable ?isSearchable .
    } optional {
        ?instance emf:isSystemProperty ?isSystemProperty .
    } optional {
        ?instance rdfs:subPropertyOf ?subPropertyOf ;
        filter (?instance != ?subPropertyOf) .
    }
}
